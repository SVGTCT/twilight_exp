title: Center Party Conference
subtitle: The Center Party chooses their new leader...
view-if: year = 1928 and month = 12 and z_party_leader = "Marx"
max-visits: 1
tags: event
new-page: true
face-image: img/Erich_Salomon_Adam_Stegerwald_Theodor_von_Guérard.jpg

= Center Party Conference

# reference: Evans, Ellen L. “Adam Stegerwald and the Role of the Christian Trade Unions in the Weimar Republic.” The Catholic Historical Review 59, no. 4 (1974): 602–26.

Wilhelm Marx, former Center Party chancellor and presidential candidate, has chosen to resign from leadership of the Center Party. Now, the party holds a conference to replace him.

[? if in_weimar_coalition: The success of the Weimar coalition has pushed the Center Party to the left. Joseph Joos, a leader of the Catholic trade unions, has emerged as the preferred candidate of the Center's liberal wing. ?][? if in_grand_coalition: The compromise through the Grand Coalition has pushed the Center Party to moderate. The consensus candidate on the right is Ludwig Kaas, a Catholic priest who represents the party's traditionalists. ?][? if in_right_coalition: The success of the right-wing coalition has pushed the Center Party to the right. The Prussian [? if prussian_government != "Z": Catholic trade unionist ?][? if prussian_government == "Z": Minister-President ?] and mentor of Heinrich Brüning, Adam Stegerwald, has long led as the race's frontrunner. ?]

- @deliberation: The Center Party will deliberate...

@deliberation
go-to: joos if in_weimar_coalition and z_relation >= 70; help_joos if in_weimar_coalition and z_relation >= 62; stegerwald if chancellor_party != "SPD" and (panzerkreuzer_no = 1 or in_right_coalition = 1) and prussian_government = "Z"; deadlock if in_weimar_coalition and chancellor = "Brüning" and prussian_government = "Z" and z_relation >= 62; kaas

@joos
on-arrival: z_party_leader = "Joos"; catholics_spd += 5; workers_z += 3
face-image: img/portraits/JoosJoseph_(crop).jpg

Owing to the Center's friendly connections with the SPD, Center delegates opine to elect Joseph Joos as party chairman, in order to maintain friendly relations, particularly at grassroots levels. He has long been a friend of the SPD, and will be more inclined to collaborate with us compared to others. He also increases the appeal of the Center Party to the working class.

@help_joos

Owing to the Center's friendly connections with the SPD, Center delegates have given Joseph Joos a fair hearing. However, the SPD's concessions have given Adam Stegerwald a commanding presence in the dispute, and he has been rallying the right against the SPD's deepest ideological differences with the Center, preventing either from carrying a majority. 

However, with our established grassroots connections, we could hold a professional campaign to elect Joos as party chairman and push him over the edge.

- @yes: Do it.
- @no: For better or for worse, we will not step in further.

@yes
subtitle: -1 resource.
choose-if: resources >= 1
on-arrival: z_party_leader = "Joos"; catholics_spd += 5; workers_z += 3
face-image: img/portraits/JoosJoseph_(crop).jpg

Joseph Joos has been elected leader of the Center Party, maintaining the SPD's friendly relations with the Catholics. He has long been a friend of the SPD, and will be more inclined to collaborate with us compared to others. He also increases the appeal of the Center Party to the working class.

@no
on-arrival: z_party_leader = "Brauns"; left_strength += 5*(1-dissent); workers_z -= 3*(1-dissent); new_middle_z -= 2*(1-dissent); old_middle_z -= 1; rural_z -= 3*(1-dissent); unemployed_z -= 3*(1-dissent); catholics_z -= 1;
face-image: img/portraits/BraunsHeinrich.jpg

Polarized beyond belief, the Joos and Stegerwald delegates refused to meet to even discuss a compromise. Eventually, the deadlock was resolved by a number of desperate Joos supporters joining forces with the Stegerwald delegates to edge out a majority for party stalwart and former Labor Minister, theologian Heinrich Brauns.

Even though Brauns is well respected, his ideas are unpopular and his organisation and management capabilities are questionable. As such the choice perplexes the entirety of the outside German political world. Furthermore, he is from the unpopular rightist wing of the party, which aims to sever the Center's connections to the pro-republic parties.

Perhaps the Center Party is in collapse?

@kaas
on-arrival: z_party_leader = "Kaas"; z_relation -= 5
face-image: img/portraits/KaasLudwig.jpg

The party does not elect the two favourites Joseph Joos and Adam Stegerwald, rather selecting Ludwig Kaas as the chairman of the Center Party, improving its ties to the bishops. He seems to represent a rightward turn from Marx, and will preside over a Center Party less inclined to compromise with the left.

@stegerwald
on-arrival: z_party_leader = "Stegerwald"; z_relation -= 10; new_middle_z += 1; old_middle_z += 2; catholics_z -= 6*(1-dissent);
face-image: img/portraits/StegerwaldCSU.jpg

Continuing with the party's rightward shift, the Center selects Adam Stegerwald as its chairman. Stegerwald is in favour of expanding the Center's reach beyond Catholics -- or "leaving the tower". Perhaps the German Catholics' political-religious polarisation, legacy of the *Kulturkampf*, will come to an end.

Elsewhere, expect very little from the Center Party to our benefit.

@deadlock
go-to: no
